plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.cheernota'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

springBoot {
    buildInfo()
}

dependencies {
    // Spring dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.4'
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'

    // Flyway
    implementation 'org.flywaydb:flyway-core'

    // Apache commons libs
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.10.0'


    // Jsoup to get and parse HTML
    implementation 'org.jsoup:jsoup:1.16.1'

    // Apache POI to make Excel report
    implementation 'org.apache.poi:poi-ooxml:5.2.3'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    compileOnly 'org.projectlombok:lombok'
    compileOnly 'org.hibernate:hibernate-jpamodelgen'

    runtimeOnly 'org.postgresql:postgresql'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
}

/* generated model folder */
sourceSets.configureEach { sourceSet ->
    tasks.named(sourceSet.compileJavaTaskName).configure {
        options.annotationProcessorGeneratedSourcesDirectory = file("$buildDir/generated/sources/java/$sourceSet.name")
    }
}
